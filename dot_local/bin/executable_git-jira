#!/usr/bin/env fish
if not set -f jira_prefix (git config jira.prefix)
    read --prompt-str "JIRA prefix: " --function jira_prefix
    set -f jira_prefix (string upper $jira_prefix)
    git config jira.prefix $jira_prefix
end
if not set -f jira_pat (git config jira.pat)
    xdg-open "https://ats.anexia-it.com/secure/ViewProfile.jspa?selectedTab=com.atlassian.pats.pats-plugin:jira-user-personal-access-tokens"
    echo "jira.pat is not set, please create an access token and call 'git config jira.pat <value>' afterwards."
    exit 1
end
set jira_id (gum input --prompt "$jira_prefix-" --char-limit=5 --header="Please enter the JIRA ticket number" --placeholder="0000")
or exit
set ticket_type (gum choose --limit=1 --header="Choose the ticket type" "feature" "hotfix" "chore" "bug")
or exit
set jira_summary (curl --silent -X GET "https://ats.anexia-it.com/rest/api/2/issue/$jira_prefix-$jira_id" -H "Authorization: Bearer $jira_pat" | jq -r '.fields.summary')
or exit
set jira_summary_slug (string join '-' $jira_summary)
set jira_summary_slug (string replace -a ' ' '-' $jira_summary_slug)
set jira_summary_slug (string lower $jira_summary_slug)
set branch_prefix JO-
set should_add_prefix (gum confirm "Add the 'JO' prefix to the branch?")
or set branch_prefix ""
set git_branch_name (gum input --width 0 --header "Name of the Git branch" --value "$ticket_type/$branch_prefix$jira_prefix-$jira_id-$jira_summary_slug")
or exit 1
git switch --create $git_branch_name origin/main
git commit --allow-empty --no-verify -m "$jira_prefix-$jira_id: $jira_summary"
